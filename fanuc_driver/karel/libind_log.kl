-- Software License Agreement (BSD License)
--
-- Copyright (c) 2012-2014, TU Delft Robotics Institute
-- All rights reserved.
--
-- Redistribution and use in source and binary forms, with or without
-- modification, are permitted provided that the following conditions
-- are met:
--
--  * Redistributions of source code must retain the above copyright
--    notice, this list of conditions and the following disclaimer.
--  * Redistributions in binary form must reproduce the above
--    copyright notice, this list of conditions and the following
--    disclaimer in the documentation and/or other materials provided
--    with the distribution.
--  * Neither the name of the TU Delft Robotics Institute nor the names 
--    of its contributors may be used to endorse or promote products 
--    derived from this software without specific prior written 
--    permission.
--
-- THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
-- "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
-- LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
-- FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE
-- COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT,
-- INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
-- BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
-- LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
-- CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
-- LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN
-- ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
-- POSSIBILITY OF SUCH DAMAGE.


PROGRAM libind_log
--------------------------------------------------------------------------------
-- 
-- Simple logging routines
-- 
-- TODO:
--   - use mutex / semaphore in 'log_clear' with init bool
-- 
-- author: G.A. vd. Hoorn (TU Delft Robotics Institute)
-- 
--------------------------------------------------------------------------------
%ALPHABETIZE
%COMMENT = 'r3'
%NOBUSYLAMP
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR




--------------------------------------------------------------------------------
-- 
-- remote types & constants
-- 
--------------------------------------------------------------------------------
%INCLUDE klevccdf




--------------------------------------------------------------------------------
-- 
-- local types & constants
-- 
--------------------------------------------------------------------------------
CONST
	-- use klevccdf constants for colours from rosconsole.cpp
	ROS_WARN  = cc_yellow
	ROS_ERR   = cc_red
	ROS_DEBUG = cc_green

	-- 
	ROS_ERR_T   = ' E '
	ROS_WARN_T  = ' W '
	ROS_INFO_T  = ' I '
	ROS_DEBUG_T = ' D '




--------------------------------------------------------------------------------
-- 
-- local routine prototypes
-- 
--------------------------------------------------------------------------------
%INCLUDE include\libind_log_h
%INCLUDE klrdutil




--------------------------------------------------------------------------------
-- 
-- Empty body
-- 
--------------------------------------------------------------------------------
BEGIN
END libind_log




--------------------------------------------------------------------------------
-- 
-- Print the current time on the current line (as prefix for log lines)
-- 
--------------------------------------------------------------------------------
ROUTINE prnt_time_
VAR
	time_ : INTEGER
	tmp_  : STRING[30]
BEGIN
	-- 
	GET_TIME(time_)
	-- keep only lower 16 bits: HH:MM:SS
	time_ = time_ AND 65535
	-- convert it to a string (raw, not using CNV_TIME_STR)
	CNV_INT_STR(time_, 4, 10, tmp_)
	-- write on current line
	WRITE TPDISPLAY(tmp_)
END prnt_time_




ROUTINE log_clear
VAR
	stat__ : INTEGER
BEGIN
	-- from klrdutil
	clear_win(TPDISPLAY, stat__)
	log_force
END log_clear




ROUTINE log_force
BEGIN
	FORCE_SPMENU(TP_PANEL, SPI_TPUSER, 1)
END log_force




ROUTINE log_info
BEGIN
	prnt_time_
	WRITE TPDISPLAY(ROS_INFO_T, msg, CR)
END log_info




ROUTINE log_info_a
BEGIN
	prnt_time_
	WRITE TPDISPLAY(ROS_INFO_T, msg, prm, CR)
END log_info_a




ROUTINE log_warn
BEGIN
	prnt_time_
	WRITE TPDISPLAY(
		CHR(cc_bgcolor), CHR(cc_black),
		CHR(cc_fgcolor), CHR(ROS_WARN),
		ROS_WARN_T, msg,
		CHR(cc_bgcolor), CHR(cc_dfcolor),
		CHR(cc_fgcolor), CHR(cc_dfcolor),
		CR)
END log_warn




ROUTINE log_warn_a
BEGIN
	prnt_time_
	WRITE TPDISPLAY(
		CHR(cc_fgcolor), CHR(ROS_WARN),
		ROS_WARN_T, msg, prm,
		CHR(cc_fgcolor), CHR(cc_dfcolor),
		CR)
END log_warn_a




ROUTINE log_error
BEGIN
	prnt_time_
	WRITE TPDISPLAY(
		CHR(cc_fgcolor), CHR(ROS_ERR),
		ROS_ERR_T, msg,
		CHR(cc_fgcolor), CHR(cc_dfcolor),
		CR)
END log_error




ROUTINE log_error_a
BEGIN
	prnt_time_
	WRITE TPDISPLAY(
		CHR(cc_fgcolor), CHR(ROS_ERR),
		ROS_ERR_T, msg, prm,
		CHR(cc_fgcolor), CHR(cc_dfcolor),
		CR)
END log_error_a
